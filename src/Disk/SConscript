#------------------------------
# IMPORTS
#------------------------------

Import('env')
Import('parallel_type')

#------------------------------
# DEPENDENCIES
#------------------------------

components = ['error']
libraries  = ['hdf5']

#------------------------------
# ENVIRONMENTS
#------------------------------

env = env.Clone(LIBS=[components,libraries])

#------------------------------
# SOURCE
#------------------------------

includes = [Glob('disk*hpp')]
sources  = [Glob('disk*cpp')]

#------------------------------
# TARGETS
#------------------------------

libraries = env.Library ('disk', sources)
test_hdf5  = env.Program (['test_FileHdf5.cpp', sources])
test_ifrit = env.Program (['test_Ifrit.cpp', sources])

if ("charm" in parallel_type):
	test = 'test_FileHdf5'
	env.CharmBuilder(test + '.decl.h',test + '.ci',ARG = test)
	env.Depends(env.Object(test + '.cpp'),test + '.decl.h')
	test = 'test_Ifrit'
	env.CharmBuilder(test + '.decl.h',test + '.ci',ARG = test)
	env.Depends(env.Object(test + '.cpp'),test + '.decl.h')

tests = [test_hdf5,test_ifrit]

#------------------------------
# INSTALL
#------------------------------

env.Alias('install-inc',env.Install ('#/include',includes))
env.Alias('install-lib',env.Install ('#/lib',libraries))
env.Alias('install-bin',env.Install ('#/bin',tests))



