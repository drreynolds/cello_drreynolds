#------------------------------
# IMPORTS
#------------------------------

Import('env')
Import('parallel_type')

#------------------------------
# DEPENDENCIES
#------------------------------

components = ['error','parameters','monitor','memory','enzo','parallel','user','enzo-ppm','data','field','cello']

libraries = ['png','boost_thread-mt','$FORTRANLIBS']

#------------------------------
# ENVIRONMENTS
#------------------------------

env = env.Clone(LIBS=[components,libraries])

#------------------------------
# SOURCE
#------------------------------

includes = [Glob('simulation*hpp')]
sources  = [Glob("simulation*cpp")]

#------------------------------
# TARGETS
#------------------------------

libraries       = env.Library ('simulation', [sources])
test_simulation = env.Program (['test_Simulation.cpp',sources])
tests           = [test_simulation];

if ("charm" in parallel_type):
	test = 'test_Simulation'
	env.CharmBuilder(test + '.decl.h',test + '.ci',ARG = test)
	env.Depends(env.Object(test + '.cpp'),test + '.decl.h')

#------------------------------
# INSTALL
#------------------------------

env.Alias('install-bin',env.Install ('#/bin', tests))
env.Alias('install-inc',env.Install ('#/include',includes))
env.Alias('install-lib',env.Install ('#/lib',    libraries))



