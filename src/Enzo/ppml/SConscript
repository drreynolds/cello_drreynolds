#------------------------------
# DIRECTORIES
#------------------------------
#------------------------------
# IMPORTS
#------------------------------

Import('env')
Import('parallel_type')

#------------------------------
# DEPENDENCIES
#------------------------------

components = ['user','field', 'parameters', 'error','disk','memory','monitor','parallel','cello']
libraries  = ['hdf5', 'png','boost_thread-mt', '$FORTRANLIBS']

#------------------------------
# ENVIRONMENTS
#------------------------------

env = env.Clone(LIBS = [components,libraries])

#------------------------------
# SOURCE
#------------------------------

sources = [
        Glob('Cosmology*cpp'),
        Glob('Grid_*cpp'),
        Glob('initialize_*cpp'),
	'../image_dump.cpp',
        '../initialize_hydro.cpp',
	'../SetExternalBoundary.cpp',
	'PPML_Conservative.F',
	'PPML_HLLD.F',
	'PPML_MAIN.F',
	'PPML_monot.F',
	'PPML_potokx.F',
	'PPML_potoky.F',
	'PPML_potokz.F',
	'PPML_Primitive.F',
	'PPML_sub.F',
	'PPML_TimeStep.F']

#------------------------------
# TARGETS
#------------------------------

binaries  = [env.Program (['test_ppml.cpp',      sources])]
libraries = env.Library ('enzo-ppml', sources)

if ("charm" in parallel_type):
	test = 'test_ppml'
	env.CharmBuilder(test + '.decl.h',test + '.ci',ARG = test)
	env.Depends(env.Object(test + '.cpp'),test + '.decl.h')

#------------------------------
# INSTALL
#------------------------------

env.Alias('install-bin',env.Install ('#/bin', binaries))
env.Alias('install-lib',env.Install ('#/lib', libraries))









