#!/bin/tcsh

#-----------------------------------------------------------------------
# Generate an input file via hypre-init, and run hypre-solve
#
# Usage: arguments are passed directly to hypre-init.  See hypre-init
#        for usage
#-----------------------------------------------------------------------

set color  = 3
set size   = 256 # png file size
set thumb  = 64

#-----------------------------------------------------------------------

set PATH = `pwd`
set bin = "$PATH/../bin"

set extra = "dump_hypre false\ndump_x true\ndump_b true"

#----------------------------------------------------------------------
# INITIALIZE PROBLEM
#----------------------------------------------------------------------

set problem = "N$1.P$2$3$4.L$5.O$6.S$7.$8.$9"

set input = in.$problem

#----------------------------------------------------------------------
# CLEAN UP PREVIOUS RUN IF ANY
#----------------------------------------------------------------------

if (-e $problem) then
   rm -rf "$problem"
endif

#----------------------------------------------------------------------
# GENERATE THE INPUT FILE
#----------------------------------------------------------------------

$bin/hypre-init $1 $2 $3 $4 $5 $6 $7 $8 $9
printf "$extra" >> $input
mv $input $problem
cd $problem

#----------------------------------------------------------------------
# RUN THE PROBLEM
#----------------------------------------------------------------------

@ np = $2 * $3 * $4 

if ($7 == 1) then
   $bin/hypre-solve $input >& outfile
else
   if ($HOST == "ds100") then
      @ nodes = ($np - 1) / 8 + 1
      if ($np < 8) then
         set procs = $np
      else
         set procs = 8
      endif
      (poe $bin/hypre-solve $input \
           -nodes          $nodes \
           -tasks_per_node $procs \
           -rmpool         1) >& outfile
   else
#      /usr/local/sbin/cleanipcs
      mpirun -np $np $bin/hypre-solve $input >& outfile
      killall -q hypre-solve
   endif
endif

#----------------------------------------------------------------------
# GENERATE PLOTS
#----------------------------------------------------------------------

set n      = $1
set levels = $5

set factor = `echo "($size / $n) / 2^($levels-1)" | bc`

$bin/hypre-png $color $factor X.? X.?? >& out.hypre-png
mv project-0.png X0.png
mv project-1.png X1.png
mv project-2.png X2.png
mv project-shift-0.png XS0.png
mv project-shift-1.png XS1.png
mv project-shift-2.png XS2.png

set factor = `echo "($thumb / $n) / 2^($levels-1)" | bc`

$bin/hypre-png $color $factor X.? X.?? >& out.hypre-png
mv project-0.png TX0.png
mv project-1.png TX1.png
mv project-2.png TX2.png
mv project-shift-0.png TXS0.png
mv project-shift-1.png TXS1.png
mv project-shift-2.png TXS2.png

#----------------------------------------------------------------------
# DISPLAY RESULT
#----------------------------------------------------------------------

set success = `grep "Success" outfile | wc`
set success = $success[1]
set norm = `awk '/norm\(X\)/{print $3}' outfile`
if ($success == 1) then
   echo "\033[01;32mPass\033[00m: $problem $norm"
else
   echo "\033[01;38mFail\033[00m: $problem $norm"
endif

rm X.* B.*


